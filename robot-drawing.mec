# Robot Arm Drawing

This is where the drawing is added to the DOM
  #app/main = [|root      direction contains|
                "drawing" "column"  [#robot-controls]
                "drawing" "column"  [#robot-animation]]

## Logic       

Set up the parameters
  #i = 2:6
  #x = [400; 0; 0; 0; 0; 0]
  #y = [450; 0; 0; 0; 0; 0]
  #angle = [#slider1{1,4}{1,3}; #slider2{1,4}{1,3}; #slider3{1,4}{1,3}]

Calculate the robot arm link coordiantes 
  i = #i
  angle1 = #angle{1,1}
  i2 = i / 2
  ir = math/floor(column: i2)
  a = #angle{ir,:}
  link-length = [53; 100; 85]
  #x{i,:} := #x{i - 1,:} + link-length{ir,:} * math/sin(degrees: a)
  #y{i,:} := #y{i - 1,:} - link-length{ir,:} * math/cos(degrees: a)

## Layout

Use the linkage coordinates to define image position and rotation
  y0 = #y{1,1} + 50
  #robot-arm = [|shape   parameters|
                 "image" [x: #x{4,1} y: #y{4,1} rotation: #angle{2,1} image: "http://mech-lang.org/img/robotarm/link2.png"]
                 "image" [x: #x{2,1} y: #y{2,1} rotation: #angle{1,1} image: "http://mech-lang.org/img/robotarm/link1.png"]
                 "image" [x: #x{1,1} y: y0      rotation: 0           image: "http://mech-lang.org/img/robotarm/link0.png"]
                 "image" [x: #x{6,1} y: #y{6,1} rotation: #angle{3,1} image: "http://mech-lang.org/img/robotarm/gripper.png"]]

Embed arm link images in canvas
  #drawing = [type: "canvas" class: _ contains: [#robot-arm] parameters: [width: 800 height: 650]]

Animation controls  
  #slider1 = [type: "slider" class: _ contains: _ parameters: [min: -120 max: 120 value: -45]]
  #slider2 = [type: "slider" class: _ contains: _ parameters: [min: -120 max: 120 value: 60]]
  #slider3 = [type: "slider" class: _ contains: _ parameters: [min: -90 max: 200 value: 170]]

Compose animation and controls
  #robot-controls = [type: "div" class: _ contains: [#slider1; #slider2; #slider3]]
  #robot-animation = [type: "div" class: _ contains: [#drawing]]