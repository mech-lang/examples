# Robot Arm Drawing

This is where the drawing is added to the DOM
  #app/main = [|root      contains|
                "drawing" [#robot-controls]
                "drawing" [#robot-animation]]

## Logic       

Set up the parameters
  #i = 2:6
  #x = [400; 0; 0; 0; 0; 0]
  #y = [450; 0; 0; 0; 0; 0]
  #angle = [#slider1{4}{3}; #slider2{4}{3}; #slider3{4}{3}]

Calculate the robot arm link coordiantes 
  ir = math/floor(column: #i / 2)
  link-length = [53; 100; 85]
  #x{#i} := #x{#i - 1} + link-length{ir} * math/sin(degrees: #angle{ir})
  #y{#i} := #y{#i - 1} - link-length{ir} * math/cos(degrees: #angle{ir})

## Layout

Use the linkage coordinates to define image position and rotation
  #robot-arm = [|shape   parameters|
                 "image" [x: #x{4} y: #y{4}      rotation: #angle{2} image: "http://mech-lang.org/img/robotarm/link2.png"]
                 "image" [x: #x{2} y: #y{2}      rotation: #angle{1} image: "http://mech-lang.org/img/robotarm/link1.png"]
                 "image" [x: #x{1} y: #y{1} + 50 rotation: 0         image: "http://mech-lang.org/img/robotarm/link0.png"]
                 "image" [x: #x{6} y: #y{6}      rotation: #angle{3} image: "http://mech-lang.org/img/robotarm/gripper.png"]]

Embed arm link images in canvas
  #drawing = [type: "canvas" contains: [#robot-arm] parameters: [width: 800 height: 650]]

Animation controls  
  #slider1 = [type: "slider" parameters: [min: -120 max: 120 value: -45]]
  #slider2 = [type: "slider" parameters: [min: -120 max: 120 value: 60]]
  #slider3 = [type: "slider" parameters: [min: -90 max: 200 value: 170]]

Compose animation and controls
  #robot-controls = [type: "div" contains: [#slider1; #slider2; #slider3]]
  #robot-animation = [type: "div" contains: [#drawing]]