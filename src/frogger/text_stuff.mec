## Boolean to stop the game if Frogger runs out of lives
  #is-game-over = false

## The position of the "game over" image
  #game-over-x-image-pos = -100
  #game-over-y-image-pos = -100

## Values to hold the score, life and time for the game
  #score = 0
  #life = 3
  #time = 15

## Update the time per tick and animate the "game over" image to appear (if the game is over)
  ~ #time/timer.ticks{1}
  #time{!#is-game-over} := #time - 1
  #game-over-x-image-pos{#is-game-over & #game-over-x-image-pos != 325} := #game-over-x-image-pos + 25
  #game-over-y-image-pos{#is-game-over & #game-over-y-image-pos != 525} := #game-over-y-image-pos + 25

## Reset the timer if it equals 0, take away a life and move Frogger to the start location
  #time{#time == 0 & !#is-game-over} := 15
  #life{#time == 0 & !#is-game-over} := #life - 1
  #frogger.y{#time == 0 & !#is-game-over} := 610

## End the game if Frogger has no lives
  #is-game-over{#life == 0} := true

## Create the text elements for the game
  title = [
    "text" [x: 10 y: 50 text: "Frogger!" fill: 0xB4D144 font: [size: 50 face: "Arial"]]
    "text" [x: 210 y: 50 text: "(but in Mech)" fill: 0xEFC162 font: [size: 30 face: "Arial"]]
    "text" [x: 400 y: 50 text: "developed by Sean Hong" font: [size: 20 face: "Arial"]]
  ]
  frog-location = [
    "text" [x: 10 y: 150 text: "Frogger's X Position:" font: [size: 40 face: "Arial"]]
    "text" [x: 400 y: 150 text: #frogger.x font: [size: 40 face: "Arial"]]
    "text" [x: 10 y: 200 text: "Frogger's Y Position:" font: [size: 40 face: "Arial"]]
    "text" [x: 400 y: 200 text: #frogger.y font: [size: 40 face: "Arial"]]
  ]
  game-elements = [
    "rectangle" [x: 5 y: 260 width: 300 height: 150 fill: 0xFFFFFF line-width: 3]
    "text" [x: 10 y: 300 text: "Score:" font: [size: 40 face: "Arial"]]
    "text" [x: 150 y: 300 text: #score font: [size: 40 face: "Arial"]]
    "text" [x: 10 y: 350 text: "Life:" font: [size: 40 face: "Arial"]]
    "text" [x: 150 y: 350 text: #life font: [size: 40 face: "Arial"]]
    "text" [x: 10 y: 400 text: "Time:" font: [size: 40 face: "Arial"]]
    "text" [x: 150 y: 400 text: #time font: [size: 40 face: "Arial"]]
    "image" [x: #game-over-x-image-pos y: #game-over-y-image-pos rotate: 0 source: "https://text2image.com/user_images/202110/text2image_T4106774_20211023_180335.png"]
  ]
  #game-text = [
    title
    frog-location
    game-elements
  ]
