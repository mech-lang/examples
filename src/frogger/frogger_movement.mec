## Booleans to avoid diagonal movement
  #is-up = false
  #is-down = false
  #is-left = false
  #is-right = false

## Execute Frogger's movement when an arrow key is pressed
  ~ #html/event/key-down
  up = #html/event/key-down.key == "ArrowUp"
  down = #html/event/key-down.key == "ArrowDown"
  left = #html/event/key-down.key == "ArrowLeft"
  right = #html/event/key-down.key == "ArrowRight"
  #is-up{up} := true
  #is-down{down} := true
  #is-left{left} := true
  #is-right{right} := true
  #frogger.vx{left & !#is-up & !#is-down} := -50
  #frogger.vx{right & !#is-up & !#is-down} := 50
  #frogger.vy{up & !#is-left & !#is-right} := -50
  #frogger.vy{down & !#is-left & !#is-right} := 50

## Stop Frogger's movement when an arrow key is released
  ~ #html/event/key-up
  up = #html/event/key-up.key == "ArrowUp"
  down = #html/event/key-up.key == "ArrowDown"
  left = #html/event/key-up.key == "ArrowLeft"
  right = #html/event/key-up.key == "ArrowRight"
  #is-up{up} := false
  #is-down{down} := false
  #is-left{left} := false
  #is-right{right} := false
  #frogger.vx{left | right} := 0
  #frogger.vy{up | down} := 0

## Update Frogger's position per tick
  ~ #time/timer.ticks{1}
  #frogger.x{!#is-game-over} := #frogger.x + #frogger.vx
  #frogger.y{!#is-game-over} := #frogger.y + #frogger.vy
