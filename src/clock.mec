Clock
======

Create a timer that ticks every second. This is the time source.
  #time/timer = [period: 1000, tick: 0, hours: 0, minutes: 0, seconds: 0]

Let's make sure the timer is working
  block/view = #time/timer.seconds

Set up a clock hands table. Degrees is the deflection from noon.
  #clock-hands = [|degrees x y stroke|
                  0       0 0  0x023963
                  0       0 0  0x023963
                  0       0 0  0xce0b46]

Update the clock
------------------

Calculate clock hand angles based on the current time.
  time = [#time/timer.hours; #time/timer.minutes; #time/timer.seconds]
  multiplier = [30; 6; 6]
  #clock-hands.degrees := multiplier * time
  
Calculate x and y endpoints
  angle = #clock-hands.degrees
  #clock-hands.x := 150 + (75 * math/sin(degrees: angle))
  #clock-hands.y := 150 - (75 * math/cos(degrees: angle))
  
Drawing
---------

Set up clock drawing elements
  #clock = [|shape    parameters|
             "circle" [cx: 150 cy: 150 radius: 100 fill: 0x0B79CE]
             "line"   [x1: 150 y1: 150 x2: #clock-hands{1,2} y2: #clock-hands{1,3} stroke: #clock-hands{1,4}]
             "line"   [x1: 150 y1: 150 x2: #clock-hands{2,2} y2: #clock-hands{2,3} stroke: #clock-hands{2,4}]
             "line"   [x1: 150 y1: 150 x2: #clock-hands{3,2} y2: #clock-hands{3,3} stroke: #clock-hands{3,4}]]

Do the draw 
  clock-canvas = [type: "canvas" class: _ contains: [#clock] parameters: [width: 300 height: 300]]
  #app/main = [root: "drawing" direction: "column" contains: [clock-canvas]]