# Robot Arm Drawing

This is where the drawing is added to the DOM
  #app/main = [|root      contains|
                "drawing" [#robot-controls]
                "drawing" [#robot-animation]]

## Logic       

Set up the parameters
  #x = [400 matrix/zeros(5)]
  #y = [450 matrix/zeros(5)]
  #angle = [#sliders.parameters.value]

Calculate the robot arm link coordiantes 
  i = 2:6
  ir = math/floor(column: i / 2)
  link-length = [53; 100; 85]
  #x{i} := #x{i - 1} + link-length{ir} * math/sin(degrees: #angle{ir})
  #y{i} := #y{i - 1} - link-length{ir} * math/cos(degrees: #angle{ir})

## Layout

Use the linkage coordinates to define image position and rotation
  url = "http://mech-lang.org/img/robotarm/"
  #robot-arm = [|shape   parameters|
                 "image" [x: #x{4} y: #y{4}       rotation: #angle{2} image: "{{url}}link2.png"]
                 "image" [x: #x{2} y: #y{2}       rotation: #angle{1} image: "{{url}}link1.png"]
                 "image" [x: #x{1} y: #y{1} + 50  rotation: 0         image: "{{url}}link0.png"]
                 "image" [x: #x{6} y: #y{6}       rotation: #angle{3} image: "{{url}}gripper.png"]]

Embed arm link images in canvas
  #drawing = [type: "canvas" contains: [#robot-arm] parameters: [width: 800 height: 650]]

Animation controls  
  #sliders = [type: "slider" parameters: [|min  max value|
                                          -120 120 -45
                                          -120 120  60
                                          -90  200  170]]

Compose animation and controls
  #robot-controls = [type: "div" contains: [#slider1; #slider2; #slider3]]
  #robot-animation = [type: "div" contains: [#drawing]]