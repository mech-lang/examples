# Pong Game Client (Server)

Define the pong ball
  #pong/ball = [x: 20 y: 20 vx: 1 vy: 3]

update ball position
  ~ #time/timer.tick
  #pong/ball.x := #pong/ball.x + #pong/ball.vx
  #pong/ball.y := #pong/ball.y + #pong/ball.vy

bounce the ball off the paddle
  ~ #pong/ball.y
  pos = #pong/paddle
  start = pos
  end = pos + 100
  ix = #pong/ball.y > 340 & #pong/ball.x > start & #pong/ball.x < end & #pong/ball.y < 342
  #pong/ball.vy{ix} := -#pong/ball.vy

bounce the ball off the ceiling
  ~ #pong/ball.y
  #pong/ball.vy{#pong/ball.y < 10} := -#pong/ball.vy

bounce the ball off the walls
  ~ #pong/ball.x
  #pong/ball.vx{#pong/ball.x > 390 | #pong/ball.x < 10} := -#pong/ball.vx

reset the ball if it makes it past the paddle
  ~ #pong/ball.y
  ix = #pong/ball.y > 390
  #pong/ball.x{ix} := 20
  #pong/ball.y{ix} := 20