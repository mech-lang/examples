## Movement

Move the ball with every tick of the timer
  ~ #pong/ticks
  #pong/ball.x := #pong/ball.x + #pong/ball.vx
  #pong/ball.y := #pong/ball.y + #pong/ball.vy

### Game Boundary Interactions

Bounce right
  ~ #pong/ticks
  ix = #pong/ball.x > (#pong/game.width - 15)
  #pong/ball.vx{ix} := -#pong/ball.vx
  #pong/ball.x{ix} := #pong/game.width - 15

Bounce left
  ~ #pong/ticks
  ix = #pong/ball.x < 15
  #pong/ball.vx{ix} := -#pong/ball.vx
  #pong/ball.x{ix} := 15

Bounce bottom
  ~ #pong/ticks
  ix = #pong/ball.y > (#pong/game.height - 15)
  #pong/ball.vy{ix} := -#pong/ball.vy
  #pong/ball.y{ix} := #pong/game.height - 15

Bounce top
  ~ #pong/ticks
  ix = #pong/ball.y < 15
  #pong/ball.vy{ix} := -#pong/ball.vy
  #pong/ball.y{ix} := 15

### Paddle Interactions

Move the desired paddle width the mouse cursor
  #pong/players.y{#control} := #html/event/pointer-move.y

Bounce the ball off the left paddle
  ~ #pong/ticks
  ix = #pong/ball.x < (#pong/players.x{1} + 20)
  iy = (#pong/ball.y > (#pong/players.y{1} - 50)) & (#pong/ball.y < (#pong/players.y{1} + 50))
  #pong/ball.vx{ix & iy} := -#pong/ball.vx
  #pong/ball.x{ix & iy} := (#pong/players.x{1} + 20)

Bounce the ball off the right paddle
  ~ #pong/ticks
  ix = #pong/ball.x > (#pong/players.x{2} - 10)
  iy = (#pong/ball.y > (#pong/players.y{2} - 50)) & (#pong/ball.y < (#pong/players.y{2} + 50))
  #pong/ball.vx{ix & iy} := -#pong/ball.vx
  #pong/ball.x{ix & iy} := (#pong/players.x{2} - 10)