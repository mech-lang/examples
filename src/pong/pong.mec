# Pong

Game elements
  #game = [width: 1000 height: 500]
  #ball = [x: 500 y: 250 vx: 5 vy: 5]
  #paddles = [|x   y|
                    50  250
                    940 300]
  #score = [left: 0 right: 0]

## Movement

Move the ball with every tick of the timer
  ~ 60Hz
  #ball.x := #ball.x + #ball.vx
  #ball.y := #ball.y + #ball.vy

### Game Boundary Interactions

Bounce right
  ix = #ball.x > (#game.width - 15)
  #ball.vx{ix} := -#ball.vx
  #ball.x{ix} := #game.width - 15
  #score.right{ix} := #score.right + 1

Bounce left
  ix = #ball.x < 15
  #ball.vx{ix} := -#ball.vx
  #ball.x{ix} := 15
  #score.left{ix} := #score.left + 1

Bounce bottom
  ix = #ball.y > (#game.height - 15)
  #ball.vy{ix} := -#ball.vy
  #ball.y{ix} := #game.height - 15
  
Bounce top
  ix = #ball.y < 15
  #ball.vy{ix} := -#ball.vy
  #ball.y{ix} := 15

### Paddle Interactions

Map player position to paddle position. The player position comes from the client.
  #paddles.y := #players.position

Bounce the ball off the left paddle
  ix = #ball.x < (#paddles.x{1} + 20)
  iy = (#ball.y > (#paddles.y{1} - 50)) & (#ball.y < (#paddles.y{1} + 50))
  #ball.vx{ix & iy} := -#ball.vx
  #ball.x{ix & iy} := #paddles.x{1} + 20

Bounce the ball off the right paddle
  ix = #ball.x > (#paddles.x{2} - 10)
  iy = (#ball.y > (#paddles.y{2} - 50)) & (#ball.y < (#paddles.y{2} + 50))
  #ball.vx{ix & iy} := -#ball.vx
  #ball.x{ix & iy} := #paddles.x{2} - 10