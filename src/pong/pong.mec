# Pong

Game elements
  #game = [width: 1000 height: 500]
  #ball = [x: 500 y: 250 vx: 5 vy: 5]
  #paddles = [|x   y|
               50  250
               940 300]
  #score = [left: 0 right: 0]

## Movement

Move the ball with every tick of the timer
  ~ 60Hz
  #ball := [x: #ball.x + #ball.vx
            y: #ball.y + #ball.vy]

### Game Boundary Interactions

Bounce right
  ix = #ball.x > (#game.width - 15)
  #ball{ix} := [x: #game.width - 15, vx: -#ball.vx]
  #score.right{ix} := #score.right + 1

Bounce left
  ix = #ball.x < 15
  #ball{ix} := [x: 15 vx: -#ball.vx]
  #score.left{ix} := #score.left + 1

Bounce bottom
  #ball{.y > (#game.height - 15)} := [y: #game.height - 15 vy: -#ball.vy]
  
Bounce top
  #ball{.y < 15} := [y: 15 vy: -#ball.vy]

### Paddle Interactions

Map player position to paddle position. The player position comes from the client.
  #paddles.y := #players.position

Bounce the ball off the left paddle
  ix = #ball.x < (#paddles.x{1} + 20)
  iy = (#ball.y > (#paddles.y{1} - 50)) & (#ball.y < (#paddles.y{1} + 50))
  #ball{ix & iy} := [x: #paddles.x{1} + 20
                     vx: -#ball.vx]

Bounce the ball off the right paddle
  ix = #ball.x > (#paddles.x{2} - 10)
  iy = (#ball.y > (#paddles.y{2} - 50)) & (#ball.y < (#paddles.y{2} + 50))
  #ball{ix & iy} := [x: #paddles.x{2} - 10
                     vx: -#ball.vx]