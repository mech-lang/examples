# Client

## Control

Default to player one
  #pong/control = 1
  #pong/players = [|position| 200; 200]

Set the player using the url hash string
  left = #html/location.hash == "1"
  right = #html/location.hash == "2"
  #pong/control{left} := 1
  #pong/control{right} := 2

Move the desired paddle width the mouse cursor
  #pong/players.position{#pong/control} := #html/event/pointer-move.y

## Drawing

Compose the scene
  circle = [
    shape: "circle" 
    parameters: [
      center-x: #pong/ball.x 
      center-y: #pong/ball.y 
      radius: 10 
      fill: 0x222222 
      line-width: 5
    ]
  ]
  paddles = [
    shape: "rectangle"
    parameters: [
      x: #pong/paddles.x
      y: #pong/paddles.y - 50
      width: 10
      height: 100
      fill: 0x222222
    ]
  ]
  background = [
    shape: "rectangle"
    parameters: [
      x: 0
      y: 0
      width: #pong/game.width
      height: #pong/game.height
      fill: 0xEEEEEE
      stroke: 0x444444
      line-width: 10
    ]
  ]
  #pong/game-board = [
    type: "canvas" 
    contains: [|shape parameters| background; paddles; circle] 
    parameters: [width: #pong/game.width height: #pong/game.height]
  ]

Attach game to root
  #html/app = [
    root: "mech-root" 
    contains: [|type contains parameters| #pong/game-board]
  ]