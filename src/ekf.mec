EKF Localization
=================

Initialize the simulation
  #time/timer += [period: 10<ms>]
  #robot = [x: 45, y: 15, θ: 0]
  #μ = [x: 55, y: 25, θ: 0.3927]
  #u = [v: 5, ω: 0.00]
  #camera = [x: 140 y: 12]
  #Σ = [100   0 0
          0 100 0
          0   0 0.1542]

1. Time Update
---------------
  
  ~ #time/timer
  θ =  #μ.θ
  Gt = [1  0 -#u.v * math/sin(angle: θ) * Δt
        0  1  #u.v * math/cos(angle: θ) * Δt
        0  0  1]
  Vt = [math/cos(angle: θ) * Δt  0
        math/sin(angle: θ) * Δt  0
        0                        Δt]
  #μ := #μ + #u.v,v,ω * [math/cos(angle: θ), math/sin(angle: θ), 1] * Δt
  #Σ := Gt ** #Σ ** Gt' + Vt ** #Q ** Vt'      


2. Measurement Update
----------------------

  ~ #time/timer
  Δy = #camera.y - #μ.y
  Δx = #camera.x - #μ.x
  q = Δx ^ 2 + Δy ^ 2
  Ẑ = math/atan2(y: Δy, x: Δx) - #μ.θ
  H = [Δy / q, -Δx / q, -1]
  S = H ** #Σ ** H' + Q
  K = #Σ ** H' / S
  #μ := (#μ + K * (#z -  Ẑ))
  #Σ := ([1 0 0; 0 1 0; 0 0 1] - K ** H) ** Σ

3. Controller
--------------

block
  ~ #time/timer
  #x :+= 1

block
  switch = #x > 400 & #x < 700
  #control.v{switch} := 0.0
  #control.ω{switch} := 0.05

block
  switch = #x > 700 & #x < 800
  #control.v{switch} := 5.0
  #control.ω{switch} := 0

5. Draw
--------

Calculate the major and minor axes of the covariance ellipse
  a = #Σ{1,1}
  b = #Σ{1,2}
  c = #Σ{2,2}
  q = (((a - c) / 2) ^ 2 + b ^ 2) ^ 0.5
  λ1 = ((a + c) / 2) + q
  #λ1 = λ1
  #λ2 = ((a + c) / 2) - q
  #θ = math/atan2(y: λ1 - a, x: b)

Define the shapes
  #robot-drawing = [
    shape: "ellipse" 
    parameters: [
      center-x: #robot.x
      center-y: #robot.y + 200
      major-axis: 16
      minor-axis: 8
      rotate: #robot.θ
      fill: 0xFF0000    
      line-width: 1.0
    ]
  ]
  #robot-hat-drawing = [
    shape: "ellipse" 
    parameters: [
      center-x: #robot-hat.x
      center-y: #robot-hat.y + 200
      major-axis: 16
      minor-axis: 8 
      rotate: #robot-hat.θ
      fill: 0x00FF00    
      line-width: 1.0
    ]
  ]
  #camera-drawing = [
    shape: "circle" 
    parameters: [
      center-x: #camera.x
      center-y: #camera.y + 200
      radius: 10.0      
      fill: 0x0000FF    
      line-width: 1.0
    ]
  ]
  #covariance = [
    shape: "ellipse" 
    parameters: [
      center-x: #robot-hat.x
      center-y: #robot-hat.y + 200
      major-axis: #λ1 ^ 0.5
      minor-axis: #λ2 ^ 0.5
      radius: 35.0      
      fill: 0xFFAEC9    
      line-width: 1.0
      rotate: #θ
    ]
  ]
  #text = [
    shape: "text" 
    parameters: [
      x: 100.0   
      y: 200.0   
      text: #x
      fill: 0xFF0000    
      font: [size: 50.0 face: "Arial"]
    ]
  ]
  
Draw a shape to the canvas
  shapes = [#covariance
            #robot-drawing
            #robot-hat-drawing
            #camera-drawing
           ]
  canvas = [
    kind: "canvas" 
    contains: [|shape parameters| shapes] 
    parameters: [width: 800.0 height: 800.0]
  ]
  #html/app = [
    root: "mech-root" 
    contains: [|kind contains parameters| canvas]
  ]